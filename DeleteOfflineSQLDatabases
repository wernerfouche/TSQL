/*When you use the SQL Server DROP DATABASE command for an offline database it only removes the database records from the SQL Server system tables, but does not remove the actual data and log files from the file system. In this tip we look at a simple and fast tool to drop all of the offline databases from your SQL Server and also delete the MDF, NDF and LDF files of these databases from their respective locations in the file system.*/

/*First we need to enable the 'Ole Automation Procedures' configuration option on your server, which can be done as follows:*/

EXEC sp_configure 'show advanced options', 1
GO
RECONFIGURE
GO
EXEC sp_configure 'Ole Automation Procedures', 1
GO
RECONFIGURE
GO

/*Here is the actual stored procedure to remove the databases.*/

use master
go

CREATE PROC usp_DetachAndDeleteOfflineDBs
AS
BEGIN
   DECLARE @filesIndex INT = 1;
   DECLARE @fileCount INT = 0;
   DECLARE @currdb SYSNAME;
   DECLARE @currfile VARCHAR(200)
   DECLARE @Result INT
   DECLARE @FSO_Token INT

   DECLARE c_dbs CURSOR
   FOR
   SELECT db.NAME
   FROM sys.databases db
   WHERE db.STATE = 6;

   SET NOCOUNT ON

   CREATE TABLE #filesToDelete 
     (
       fid INT identity
      ,filepath VARCHAR(200)
      );

   INSERT INTO #filesToDelete
   SELECT mf.physical_name
   FROM sys.databases db
   INNER JOIN sys.master_files mf ON db.database_id = mf.database_id
   WHERE db.STATE = 6;

   SELECT @fileCount = count(*)
   FROM #filesToDelete;

   OPEN c_dbs

   FETCH NEXT FROM c_dbs INTO @currdb

   WHILE @@FETCH_STATUS = 0
   BEGIN
      EXEC sp_detach_db @dbname = @currdb

      FETCH NEXT FROM c_dbs INTO @currdb
   END

   CLOSE c_dbs
   DEALLOCATE c_dbs

   EXEC @Result = sp_OACreate 'Scripting.FileSystemObject', @FSO_Token OUTPUT

   WHILE @filesIndex <= @fileCount
   BEGIN
      SELECT @currfile = filepath
      FROM #filesToDelete
      WHERE fid = @filesIndex;

      EXEC @Result = sp_OAMethod @FSO_Token, 'DeleteFile', NULL, @currfile

      SET @filesIndex = @filesIndex + 1;
   END

   EXEC @Result = sp_OADestroy @FSO_Token
   
   DROP TABLE  #filesToDelete
 
   SET NOCOUNT OFF
END
GO

/*Using the Stored Procedure to Remove Offline Databases and File*/

use master
go
exec dbo.usp_DetachAndDeleteOfflineDBs 
go

